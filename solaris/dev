#! /bin/csh -f

# For Tasvir and EmbeddedTk.
set path = ( $path /p/qclic/Code/Qclic/bin.common )
set path = ( $path /p/devise/public/bin )

setenv DEVISE_ROOT		/p/devise

# Set up the 'standard' data, sessions, etc., so these will be used
# if something is missing from the demo or personal versions.
setenv DEVISE_DAT		$DEVISE_ROOT/dat
setenv DEVISE_HOME_TABLE	$DEVISE_ROOT/schema/schema/catalog.dte
setenv DEVISE_SCHEMA		$DEVISE_ROOT/schema/schema
setenv DEVISE_SESSION		$DEVISE_ROOT/session
setenv DEVISE_PALETTE		$DEVISE_ROOT/palette

if ($#argv > 0) then
  if ("$1" == "p") then
    # Use personal data, sessions, etc.
    set personal_file = ~/.dev.personal
    if (-f $personal_file) then
      source $personal_file
      #TEMP
      #setenv DEVISE_HOME_TABLE	./catalog.dte
    else
      echo "DEVise personal info file $personal_file doesn't exist"
      exit 1
    endif
    shift

  else if ("$1" == "d") then
    # Use demo data, sessions, etc.
    setenv DEVISE_DAT		$DEVISE_ROOT/demo/dat
    setenv DEVISE_HOME_TABLE	$DEVISE_ROOT/demo/schema/schema/catalog.dte
    #setenv DEVISE_CACHE   	$DEVISE_ROOT/cache
    setenv DEVISE_SCHEMA  	$DEVISE_ROOT/demo/schema/schema
    setenv DEVISE_SESSION 	$DEVISE_ROOT/demo/session
    setenv DEVISE_PALETTE	$DEVISE_ROOT/demo/palette
    shift

  else if ("$1" == "j") then
    # Use JavaScreen demo data, sessions, etc.
    setenv DEVISE_DAT		$DEVISE_ROOT/demo/dat
    setenv DEVISE_HOME_TABLE	$DEVISE_ROOT/demo/schema/schema/catalog.js.dte
    #TEMP
    #setenv DEVISE_HOME_TABLE	$DEVISE_ROOT/demo/schema/schema/catalog.js.dte.test
    #TEMP
    #setenv DEVISE_HOME_TABLE	$DEVISE_ROOT/demo/schema/schema/catalog.soil_test.dte
    #setenv DEVISE_CACHE   	$DEVISE_ROOT/cache
    setenv DEVISE_SCHEMA  	$DEVISE_ROOT/demo/schema/schema
    setenv DEVISE_SESSION 	$DEVISE_ROOT/demo/session.js
    setenv DEVISE_PALETTE	$DEVISE_ROOT/demo/palette
    shift

  else if ("$1" == "l") then
    # Use local data, sessions, etc.
    setenv DEVISE_DAT		../public/dat
    setenv DEVISE_HOME_TABLE	../public/schema/schema/catalog.dte
    #setenv DEVISE_CACHE   	$DEVISE_ROOT/cache
    setenv DEVISE_SCHEMA  	../public/schema/schema
    setenv DEVISE_SESSION 	../public/session
    setenv DEVISE_PALETTE	../public/palette
    shift

  else if ("$1" == "dist") then
    # Use distribution data, sessions, etc., as in this CVS tree (stuff
    # that will go into the demo area distribution)
    setenv DEVISE_DAT		../dist/demo/dat
    setenv DEVISE_HOME_TABLE	../dist/demo/schema/schema/catalog.dte
    #setenv DEVISE_CACHE   	$DEVISE_ROOT/cache
    setenv DEVISE_SCHEMA  	../dist/demo/schema/schema
    setenv DEVISE_SESSION 	../dist/demo/session
    setenv DEVISE_PALETTE	../public/palette
    shift

  else if ("$1" == "u") then
    echo "Usage: dev|mul [p|d|j|l|u] [DEVise args]"
    exit 1
  endif
endif

setenv DEVISE_TMP		/tmp/$USER-devise-tmp
setenv DEVISE_WORK		/tmp/$USER-devise-work
setenv LM_DAT			/tmp/LM/dat
setenv LM_TMP			/tmp/LM/dat
setenv DEVISE_LOG_DIR		$DEVISE_TMP

setenv DEVISE_LIB		$cwd/../lib
setenv LM_LIB			$DEVISE_LIB/LM
if ("$0" == "mul") then
  setenv DEVISE_EXEC		$cwd/multi/disp
else
  setenv DEVISE_EXEC		$cwd/generic/generic
endif

if (! -f $DEVISE_EXEC) then
  echo "Cannot find Devise executable. Perhaps this platform is not supported?"
  exit 1
endif
$DEVISE_EXEC $*
#$DEVISE_EXEC $* -logLevel 7

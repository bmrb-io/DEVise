#! /bin/csh -f

# Test bug 167 (make_view and make_uvd allow arbitrary command execution).

set result = 0

rm -r -f foobar
rm -r -f barfoo
rm tmp/s2d.out
rm tmp/uvd.out

echo -n "Processing NMR-STAR: "
# Note: need single quotes here to prevent backtick expansion!
set args = 'number=4267&force=1&dist_url=`touch%20foobar`'
./make_view $args >& test_bug167.out1
if ($status != 0) then
  echo "First invocation of make_view failed!"
  set result = 1
endif

echo -n "Processing NMR-STAR: "
# Note: need single quotes here to prevent backtick expansion!
set args = 'file=test_data/bmr4267.str&name=test_bug167_out&force=1&dist_url=`touch%20barfoo`'
./make_uvd $args >& test_bug167.out2
if ($status != 0) then
  echo "First invocation of make_uvd failed!"
  set result = 1
endif


echo "Checking output files"

if (-e foobar) then
  echo "Embedded command was run (found foobar)!"
  set result = 1
endif

if (-e barfoo) then
  echo "Embedded command was run (found barfoo)!"
  set result = 1
endif


# Make sure make_view and make_uvd actually got run...
set files = (tmp/s2d.out tmp/uvd.out)

foreach file ($files)
  if (! -f $file) then
    echo "  Output file $file is missing"
    set result = 1
  endif
end

echo -n "test_bug167 result: "
if ($result == 0) then
  echo "OK"
  exit 0
else
  echo "FAILED"
  exit 1
endif

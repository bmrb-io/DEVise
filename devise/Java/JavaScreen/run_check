#! /bin/csh -f

############################################################

# This file is just to get the path, etc., set up right to run check_jsall
# from cron.  It also fixes the problem that output from cron jobs
# is not mailed in some versions of Solaris.

#-----------------------------------------------------------

# $Id$

############################################################

set start_time = `date`

set idstr = regular
if ($#argv > 0) then
  if (xx$1 == xx-dev) then
    sleep 5
    if ($#argv > 1) then
      set idstr = $2
    endif
  else
    set idstr = $1
  endif
endif

set outfile = /tmp/run_check.out:`./get_timestamp`:$idstr
set outfile2 = /tmp/run_check.out2:`./get_timestamp`:$idstr

if (-e $outfile) then
    rm $outfile
endif

./check_jsall $* >& $outfile

# All this garbage because Sun's current crond on Intel/Solaris won't
# send mail correctly.
if (-e $outfile && ! -z $outfile) then
    if (-e $outfile2) then
        rm $outfile2
    endif
    set header = "Check_jsall ($idstr/`hostname`) error"
    echo $header > $outfile2
    echo "run_check started at $start_time" >> $outfile2
    echo "" >> $outfile2
    cat $outfile >> $outfile2
    if (`hostname` == "yola") then
        # Note: stupid mail on SGI doesn't take -s argument!
        cat $outfile2 | mail `cat scripts/error_email`
    else
        cat $outfile2 | mail -s "$header" `cat scripts/error_email`
    endif
    mv $outfile2 logs
endif

rm $outfile

set tdir = /tmp/.X11-unix
set tfile = js_check.$idstr
if (-d $tdir) then
  touch $tdir/$tfile
else
  touch /tmp/$tfile
endif

#! /bin/csh -f

############################################################

# Restarts the jspop and associated processes, killing any
# existing ones.

#-----------------------------------------------------------

# $Id$

############################################################

# Check the arguments.
if ($#argv == 1 || $#argv == 3 || $#argv > 4) then
  echo "Usage: restart_jspop [id_string server_count [jss_host jss_path]]"
  exit
endif

set idStr = "regular"
set serverCount = 1
if ($#argv >= 1) then
  set idStr = $argv[1]
  set serverCount = $argv[2]
endif

set jssHost = ""
set jssPath = ""
if ($#argv >= 3) then
  set jssHost = $argv[3]
  set jssPath = $argv[4]
endif


# Get the port numbers and log file names.
set info = `./ports+files $idStr`
set cmdPort = `echo $info | cut -d' ' -f1`
set popPort = `echo $info | cut -d' ' -f2`
set popFile = `echo $info | cut -d' ' -f4`


echo "Restarting jspop on `hostname`."


# Kill the jspop if it's running.
./jspop.kill $idStr


# Rename jspop log file.
set timestamp = `./get_timestamp`
set popFile_new = $popFile.$timestamp
echo "Saving log file as $popFile_new"
mv $popFile $popFile_new


# Restart the jspop.
sleep 15

#./jspop -id$idStr -cmdport$cmdPort -jspopport$popPort -restore
./jspop -id$idStr -cmdport$cmdPort -jspopport$popPort 

# Restart the jss.
if ($jssHost == "") then
  ./restart_jss $idStr $serverCount
else
  # Note: background the whole rsh command because rsh won't return until
  # the jss started by this command dies(!).  RKW 2000-12-08.
  rsh $jssHost "cd $jssPath; ./restart_jss $idStr $serverCount `hostname`" &
endif

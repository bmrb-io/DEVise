#! /bin/csh

############################################################

# Restarts the jspop and associated processes, killing any
# existing ones.

#-----------------------------------------------------------

# $Id$

############################################################

# Check the arguments.
if ($#argv == 2 || $#argv > 3) then
  echo "Usage: check_jsall [id_string [jss_host jss_path]]"
  exit
endif

set idStr = "regular"
if ($#argv >= 1) then
  set idStr = $argv[1]
endif

set jssHost = ""
set jssPath = ""
if ($#argv >= 3) then
  set jssHost = $argv[2]
  set jssPath = $argv[3]
endif


# Get the port numbers and log file names.
set info = `ports+files $idStr`
set cmdPort = `echo $info | cut -d' ' -f1`
set imgPort = `echo $info | cut -d' ' -f2`
set popPort = `echo $info | cut -d' ' -f3`
set popFile = `echo $info | cut -d' ' -f5`


echo "Restarting jspop on `hostname`."


# Kill the jspop if it's running.
jspop.kill -id$idStr


# Rename jspop log file.
set timestamp = `get_timestamp`
set popFile_new = $popFile.$timestamp
echo "Saving log file as $popFile_new"
mv $popFile $popFile_new


# Restart the jspop.
sleep 15
jspop -id$idStr -cmdport$cmdPort -imgport$imgPort -jspopport$popPort


# Restart the jss.
if ($jssHost == "") then
  restart_jss $idStr
else
  # Note: background the whole rsh command because rsh won't return until
  # the jss started by this command dies(!).  RKW 2000-12-08.
  rsh $jssHost "cd $jssPath; restart_jss $idStr `hostname`" &
endif

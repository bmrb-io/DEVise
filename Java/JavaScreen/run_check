#! /bin/csh -f

############################################################

# This file is just to get the path, etc., set up right to run check_jsall
# from cron.  It also fixes the problem that output from cron jobs
# is not mailed in some versions of Solaris.

#-----------------------------------------------------------

# $Id$

############################################################


# For CS.
set path = ( /s/std/bin /usr/ucb /bin /p/devise/run . $path )

# For yola.
set path = ( $path /usr/people/zoo/bin /usr/local/zoobin /usr/freeware/bin /usr/people/zoo/zoobin /usr/sbin /usr/bsd /sbin /usr/bin /usr/bin/X11 /usr/freeware/bin /bmrb/bin /bmrb/irix/bin /usr/java/bin /usr/local/bin /usr/people/zoo/perl /usr1/local/share /usr1/local/bin /usr/bin/X11 )

if ($#argv > 0) then
  set idstr = $1
else
  set idstr = regular
endif

set outfile = /tmp/run_check.out.$idstr

if (-e $outfile) then
    rm $outfile
endif

./check_jsall $* >& $outfile

# All this garbage because Sun's current crond on Intel/Solaris won't
# send mail correctly.
if (-e $outfile && ! -z $outfile) then
    set tmpfile = tmp.$idstr
    if (-e $tmpfile) then
        rm $tmpfile
    endif
    set header = "Error message from check_jsall ($idstr) on `hostname`"
    echo $header > $tmpfile
    cat $outfile >> $tmpfile
    if (`hostname` == "yola") then
        # Note: stupid mail on SGI doesn't take -s argument!
        cat $tmpfile | mail `cat scripts/error_email`
    else
        cat $tmpfile | mail -s "$header" `cat scripts/error_email`
    endif
    mv $tmpfile $outfile
endif

touch /tmp/js_check
touch ./js_check

#! /bin/csh -f

############################################################

# Check all JavaScreen-related processes and restart them
# if necessary.

#-----------------------------------------------------------

# $Id$

############################################################

# Check the arguments.
set dev = ""
if (xx$1 == "xx-dev") then
  set dev = "-dev"
  shift
endif

if ($#argv == 1 || $#argv == 3 || $#argv > 4) then
  echo "Usage: check_jsall [-dev] [id_string server_count [jss_host jss_path]]"
  exit
endif

set idStr = "regular"
set serverCount = 1
if ($#argv >= 1) then
  set idStr = $argv[1]
  set serverCount = $argv[2]
endif

set jssHost = ""
set jssPath = ""
if ($#argv >= 3) then
  set jssHost = $argv[3]
  set jssPath = $argv[4]
endif


# Check whether all of the processes are running.
set popCheck = `./check_jspop $dev $idStr`
if ($jssHost == "") then
  set jssCheck = `./check_jss $idStr`
else
  set jssCheck = `rsh $jssHost "cd $jssPath; ./check_jss $idStr"`
endif

set date = `date`
if ($popCheck != "OK") then
  echo "check_jsall:  no jspop process found (at $date)"
endif

if ($jssCheck != "OK") then
  echo "check_jsall:  no jss/devised processes found (at $date)"
endif

if ($popCheck != "OK" || $jssCheck != "OK") then
  echo ""
  echo -n "Uptime: "
  uptime
  echo ""

  ./restart_jspop $idStr $serverCount $jssHost $jssPath
endif

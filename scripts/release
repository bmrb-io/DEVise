#! /bin/csh -f

#  ========================================================================
#  DEVise Data Visualization Software
#  (c) Copyright 2001
#  By the DEVise Development Group
#  Madison, Wisconsin
#  All Rights Reserved.
#  ========================================================================
#
#  Under no circumstances is this software to be copied, distributed,
#  or altered in any way without prior permission from the DEVise
#  Development Group.

#  Release DEVise.

############################################################

#  $Id$

#  $Log$

############################################################

#-----------------------------------------------------------
# Get and check command-line arguments.

if ($#argv < 2) then
  echo "Usage: release <destination> <arch1> [arch2] ..."
  echo "  Available architectures:"
  echo "    solaris (Solaris/Intel),"
  echo "    solsparc (Solaris/SPARC),"
  echo "    linux (Linux/Intel)"
  exit 1
endif

set dest = $1/release
set arches = ($argv[2-$#argv])

#-----------------------------------------------------------
# Make sure we have the files we need to do the release.

set files = (lib/control.tk doc/userman.ps doc/sysman.ps \
    doc/Agreement doc/Copyright doc/Disclaimer)
foreach file ($files)
  if (! -f $file) then
    echo "File $file missing."
    echo "Cannot proceed with release procedure."
    exit 1
  endif
end

set archfiles = (generic/generic generic/devise generic/deviseb \
    generic/devised)

foreach arch ($arches)
  foreach file ($archfiles)
    if (! -f $arch/$file) then
      echo "File $arch/$file missing."
      echo "Cannot proceed with release procedure."
      exit 1
    endif
  end
end

#-----------------------------------------------------------
# Confirm that we want to go ahead.

echo ""
echo "You are about to release YOUR copy of DEVise to the"
echo "general public. The destination directory is:"
echo "  $dest"
echo ""
echo "The release includes $arches executables,"
echo "Tcl/Tk code, and user and other documentation."
echo ""
echo -n "Are you absolutely sure you want to do this? [N] "

set answer = $<
if ($answer != y && $answer != Y) then
  echo Aborted.
  exit 1
endif

echo ""

#-----------------------------------------------------------
# Create bin directories and copy executables.

echo "Creating bin directories"

if (! -d $dest) then
  mkdir $dest
endif

if (! -e $dest/bin) then
  ln -s bin_arch/@sys $dest/bin
endif

if (! -d $dest/bin_arch) then
  mkdir $dest/bin_arch
endif

foreach arch ($arches)
  echo "Releasing DEVise $arch executables"

  if ($arch == linux) then
    set mainbin = i386_linux
    set otherbins = (i386_linux22 i386_rh61 i386_rh62)
  else if ($arch == solaris) then
    set mainbin = sunx86_54
    set otherbins = (sunx86_55 sunx86_56 sunx86_57)
  else if ($arch == solsparc) then
    set mainbin = sun4m_54
    set otherbins = (sun4m_55 sun4x_55 sun4x_56 sun4x_57)
  endif

  pushd $dest/bin_arch > /dev/null

  if (! -d $mainbin) then
    mkdir $mainbin
  endif

  foreach dir ($otherbins)
    if (! -e $dir) then
      ln -s $mainbin $dir
    endif
  end

  popd > /dev/null

  cp -p $arch/generic/generic $dest/bin_arch/$mainbin/devise
  cp -p $arch/generic/devise $dest/bin_arch/$mainbin/devisec
  cp -p $arch/generic/deviseb $dest/bin_arch/$mainbin
  cp -p $arch/generic/devised $dest/bin_arch/$mainbin
  chmod a+rz $dest/bin_arch/$mainbin/*
end

#-----------------------------------------------------------
# Release the Tcl/Tk (GUI) code.

echo ""
echo "Releasing Tcl/Tk code"
if (! -d $dest/lib) then
  mkdir $dest/lib
endif
cp -p lib/[a-z]* $dest/lib
cp -p lib/[A-Z]*.* $dest/lib
chmod a+r $dest/lib/*


#-----------------------------------------------------------
# Release run files.

echo ""
echo "Releasing run files"
if (! -d $dest/run) then
  mkdir $dest/run
endif
cp -p run.new/* $dest/run

#-----------------------------------------------------------
# Release documentation.

echo ""
echo "Releasing documentation"
if (! -d $dest/doc) then
  mkdir $dest/doc
  endif
cp -p doc/*.ps doc/*.txt $dest/doc
cp -p doc/Agreement $dest/doc
cp -p doc/Copyright $dest/doc
cp -p doc/Disclaimer $dest/doc

#-----------------------------------------------------------
# Set up public directory.

tar cv --exclude=CVS public | tar xv --directory=$dest

#! /bin/csh -f

#  ========================================================================
#  DEVise Data Visualization Software
#  (c) Copyright 2001
#  By the DEVise Development Group
#  Madison, Wisconsin
#  All Rights Reserved.
#  ========================================================================
#
#  Under no circumstances is this software to be copied, distributed,
#  or altered in any way without prior permission from the DEVise
#  Development Group.

############################################################

#  Install the DEVise Condor user data scripts.

############################################################

#  $Id$

############################################################

#-----------------------------------------------------------
# Check and get the arguments.

if ($#argv > 1) then
  echo "Usage: install [target]"
  exit 1;
endif

if ($#argv > 0) then
  set target = $1
else
  set target = "/p/devise/demo/CondorData"
endif

#-----------------------------------------------------------
# Perform various checks before we actually do anything.

# Make sure the user really wants to do this.

echo ""
echo "You are about to install the DEVise Condor data setup.  This"
echo "will install files in $target."
echo ""
echo -n "Are you absolutely sure you want to do this? [n] "

set answer = $<
if ($answer != y && $answer != Y) then
  echo "Aborted."
  exit 1
endif

echo ""

#-----------------------------------------------------------
# Okay, here we actually start doing the installation.

# Create the target directory if necessary.

if (! -e $target) then
  echo "Making target directory ($target)."
  mkdir $target
endif


# Create the session links if necessary.

pushd $target > /dev/null

if (! -e UserMonth.ds) then
  echo "Creating UserWeek.ds session link."
  ln -s ../session.js/Condor/UserWeek.ds UserMonth.ds
endif

if (! -e UserThumb.ds) then
  echo "Creating UserThumb.ds session link."
  ln -s ../session.js/Condor/UserThumb.ds .
endif

popd > /dev/null


# Copy the scripts to the target directory.

echo "Copying DEVise Condor data scripts."

set files = (condor_devise_userdata create_2users create_condor_sessions create_user_thumbnails cron_entry run_condor_update split_user_data)
foreach file ($files)
  cp $file $target
end

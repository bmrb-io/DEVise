#  This file based on John Ousterhout's Tcl/Tk book and modified
#  locally.

############################################################

#	$Id$

#	$Log$
#	Revision 1.6  1995/11/10 21:55:48  jussi
#	Minor fix.
#
#	Revision 1.5  1995/11/10 21:45:08  jussi
#	Changed type of text message from 'text' to 'message'.
#
#	Revision 1.4  1995/09/22 17:33:16  jussi
#	Added ownership message.
#
#	Revision 1.3  1995/09/21 15:35:56  jussi
#	Minor changes.
#
#	Revision 1.2  1995/09/05 22:47:56  jussi
#	Added/updated CVS header.

############################################################

proc dialog {w title text bitmap default args} {
	global button

	# 1. Create the top-level window and divide it into top
	# and bottom parts.

	toplevel $w -class Dialog
	wm geometry $w +100+100
	wm title $w $title
	wm iconname $w Dialog
	frame $w.top -relief raised -bd 1
	pack $w.top -side top -fill both
	frame $w.bot -relief raised -bd 1
	pack $w.bot -side bottom -fill both

	# 2. Fill the top part with the bitmap and message.

#	set txtWidth [LineWidth $text]
#	set txtHeight [LineHeight $text]
#	text $w.top.msg -font -*-*-medium-r-normal-*-140-*-*-m-* \
#		-width $txtWidth -height $txtHeight \
#		-relief flat
#	$w.top.msg insert end $text
#	$w.top.msg configure -state disabled
	message $w.top.msg -justify center -width 8c -text $text
	pack $w.top.msg -side right -expand 1 -fill both\
			-padx 3m -pady 3m
	if {$bitmap != ""} {
		label $w.top.bitmap -bitmap $bitmap
		pack $w.top.bitmap -side left -padx 3m -pady 3m
	}

	# 3. Create a row of buttons at the bottom of the dialog.

	set i 0
	foreach but $args {
		button $w.bot.button$i -text $but -command\
				"set button $i"
		if {$i == $default} {
			frame $w.bot.default -relief sunken -bd 1
			raise $w.bot.button$i
	 		pack $w.bot.default -side left -expand 1 -fill x 
#					-padx 3m -pady 2m
			pack $w.bot.button$i -in $w.bot.default -side left -expand 1 \
				-fill x -padx 1m -pady 1m
#				-padx 2m -pady 2m -ipadx 2m -ipady 1m
		} else {
			pack $w.bot.button$i -side left -expand 1 -fill x 
#					-padx 3m -pady 3m -ipadx 2m -ipady 1m
		}
		incr i
	}

	# 4. Set up a binding for <Return>, if there`s a default,
	# set a grab, and claim the focus too.

	if {$default >= 0} {
		bind $w <Return> "$w.bot.button$default flash; \
			set button $default"
	}

	set oldFocus [focus]
	tkwait visibility $w
	grab set $w
	focus $w

	# 5. Wait for the user to respond, then restore the focus
	# and return the index of the selected button.

	tkwait variable button
	destroy $w
	focus $oldFocus
	return $button
}

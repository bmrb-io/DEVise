#! /bin/csh -f

#  ========================================================================
#  DEVise Data Visualization Software
#  (c) Copyright 2000-2001
#  By the DEVise Development Group
#  Madison, Wisconsin
#  All Rights Reserved.
#  ========================================================================
#
#  Under no circumstances is this software to be copied, distributed,
#  or altered in any way without prior permission from the DEVise
#  Development Group.

############################################################

#  Install the peptide-cgi setup.  Note: some of the installation is
#  specific to a certain machine; this script is configured to do
#  the installation on either pumori (cs) or aden (bmrb).  Changes
#  may be needed to do the installation on other machines.

#  Note: CS install must be done on pumori; BMRB on aden.

#  To use the installed software, go to:
#  http://bmrb.wisc.edu/devise/peptide-cgi/bmrb_select.html
#    or
#  http://pumori.cs.wisc.edu/bmrb/bmrb_select.html.

############################################################

#  $Id$

############################################################

# Check the argument.

if ($#argv == 1) then
  set location = $1
  if ($location == cs) then
    set demohome = /p/devise/demo
    set wwwhome = /var/home/www
    set pephome = $wwwhome/public/bmrb
    set cgihome = $wwwhome/cgi-bin/bmrb
    set sessionhome = /local.pumori/devise/bmrb/dynamic_sessions
    set datahome = /local.pumori/devise/bmrb/dynamic_data

    # Check whether we're on the appropriate machine.
    if (`hostname` != "pumori.cs.wisc.edu") then
      echo "cs installation is normally done on pumori."
      echo -n "Are you sure you want to continue? [n] "

      set answer = $<
      if ($answer != y && $answer != Y) then
        echo Aborted.
        exit 1
      endif
    endif

  else if ($location == bmrb) then
    set demohome = /p/devise/demo
    set wwwhome = /bmrb/htdocs
    set pephome = $wwwhome/devise/peptide-cgi
    set cgihome = $wwwhome/cgi-bin/peptide-cgi
    set sessionhome = $pephome/session_dir
    set datahome = $pephome/data_dir

    # Check whether we're on the appropriate machine.
    if (`hostname` != "aden.bmrb.wisc.edu") then
      echo "cs installation is normally done on aden."
      echo -n "Are you sure you want to continue? [n] "

      set answer = $<
      if ($answer != y && $answer != Y) then
        echo Aborted.
        exit 1
      endif
    endif

    # Check whether we've modified the files to work at BMRB.
    echo "Have you modified cgi-bin/make_view and bmrb_select.html"
    echo -n "for installation at bmrb? [n]"
    set answer = $<
    if ($answer != y && $answer != Y) then
      echo Aborted.
      exit 1
    endif
  else
    echo "Illegal argument value: $location"
    exit 1
  endif
else
  echo "Usage: install <cs|bmrb>"
  exit 1
endif


# Check that the Java code is compiled.
set file = "java_classes/S2DMain.class"
if (! -f $file) then
  echo "Please compile the Java code before installing."
  exit 1
endif


# Make sure the user really wants to do this.

echo You are about to install the peptide-cgi setup.  This
echo will install files in $demohome, $wwwhome,
echo and $pephome.
echo ""
echo -n "Are you absolutely sure you want to do this? [n] "

set answer = $<
if ($answer != y && $answer != Y) then
  echo Aborted.
  exit 1
endif


# Create and set up the directory containing the "front-end" html file.

set targetdir = $pephome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rx $targetdir
cp -p bmrb_select.html $targetdir
chmod a+r $targetdir/bmrb_select.html


# Create and set up the dynamic sessions directory.

set targetdir = $sessionhome
if (! -e $targetdir) then
  mkdir $targetdir

  # Needed so www-user can write here.
  fs setacl $targetdir system:anyuser write
endif
chmod a+rwx $targetdir
cp -p dynamic_sessions/*.base $targetdir
chmod a+r $targetdir/*.base

cp -p dynamic_sessions/*.str $demo/session.js/bmrb
chmod a+r $demo/session.js/bmrb/*.str


# Create and set up the data/html directory.

set targetdir = $datahome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rwx $targetdir
cp -p html/* $targetdir
find $targetdir -name \* -type f -exec chmod a+r {} \;


# Create the link that we use to have the browser find the summary page.

if ($location == cs) then
  set targetdir = $wwwhome/public/bmrb/html
  if (! -e $targetdir) then
    ln -s $demohome/dat/bmrb/dynamic_data $targetdir
  endif
endif


# Create and set up the CGI directory.

set targetdir = $cgihome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rwx $targetdir
cp -p cgi-bin/make_view $targetdir
chmod a+rx $targetdir/make_view
cp -p cgi-bin/s2d $targetdir
chmod a+rx $targetdir/s2d
cp -p cgi-bin/set_modes $targetdir
chmod a+rx $targetdir/set_modes
cp -p cgi-bin/s2d.props $targetdir
chmod a+rx $targetdir/s2d.props


# Link data directory to CGI directory.

set targetdir = $cgihome/data_dir
if (! -e $targetdir) then
  ln -s $datahome $targetdir
endif


# Link session directory to CGI directory.

set targetdir = $cgihome/session_dir
if (! -e $targetdir) then
  ln -s $sessionhome $targetdir
endif


# Link data directory to the demo area.

set targetdir = $demohome/dat/bmrb/dynamic_data
if (! -e $targetdir) then
  ln -s $datahome $targetdir
endif


# Link session directory to the demo area.

set targetdir = $demohome/session.js/bmrb/dynamic_sessions
if (! -e $targetdir) then
  ln -s $sessionhome $targetdir
endif


# Create and set up the java classes directory.

set targetdir = $cgihome/java_classes
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rx $targetdir
cp java_classes/*.class $targetdir
cp java_classes/*.jar $targetdir
chmod a+r $targetdir/*.*


# Copy the schemas to the demo area.

cp schema/bmrb* $demohome/schema/schema/physical

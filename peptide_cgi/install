#! /bin/csh -f

#  ========================================================================
#  DEVise Data Visualization Software
#  (c) Copyright 2000-2001
#  By the DEVise Development Group
#  Madison, Wisconsin
#  All Rights Reserved.
#  ========================================================================
#
#  Under no circumstances is this software to be copied, distributed,
#  or altered in any way without prior permission from the DEVise
#  Development Group.

############################################################

#  Install the peptide-cgi setup.  Note: some of the installation is
#  specific to a certain machine; this script is configured to do
#  the installation on either pumori (cs) or aden (bmrb).  Changes
#  may be needed to do the installation on other machines.

#  Note: CS install must be done on pumori; BMRB on aden.

#  To use the installed software, go to:
#  http://bmrb.wisc.edu/devise/peptide-cgi/bmrb_select.html
#    or
#  http://pumori.cs.wisc.edu/bmrb/bmrb_select.html.

############################################################

#  $Id$

############################################################

#-----------------------------------------------------------
# Check and get the arguments.

if ($#argv < 6 || $#argv > 7) then
  echo "Usage: install <dev_demo_home> <www_home> <pep_home> <cgi_home>"
  echo "  <pep_session_home> <pep_data_home> [install_machine]"
  exit 1;
endif

set demohome = $1
set wwwhome = $2
set pephome = $3
set cgihome = $4
set sessionhome = $5
set datahome = $6

#-----------------------------------------------------------
# Perform various checks before we actually do anything.

if ($#argv == 7) then
  # Check whether we're on the appropriate machine.

  if (`hostname` != $7) then
    echo "Installation is normally done on $7."
    echo -n "Are you sure you want to continue? [n] "

    set answer = $<
    if ($answer != y && $answer != Y) then
      echo Aborted.
      exit 1
    endif
  endif
endif


# Check that the Java code is compiled.

set file = "java_classes/S2DMain.class"
if (! -f $file) then
  echo "Please compile the Java code before installing."
  exit 1
endif


# Make sure other necessary files have been generated.

if (! -f bmrb_select.html || ! -f cgi-bin/s2d.props || ! -d cgi-bin/bin) then
  echo "You must do 'make setup' before doing 'make install'."
  exit 1
endif


# Make sure the demo home and www home directories already exist.

if (! -d $demohome) then
  echo "$demohome directory doesn't exist; aborting."
  exit 1
endif
if (! -d $wwwhome) then
  echo "$wwwhome directory doesn't exist; aborting."
  exit 1
endif


# Make sure the user really wants to do this.

echo You are about to install the peptide-cgi setup.  This
echo will install files in $demohome, $wwwhome,
echo and $pephome.
echo ""
echo -n "Are you absolutely sure you want to do this? [n] "

set answer = $<
if ($answer != y && $answer != Y) then
  echo Aborted.
  exit 1
endif

#-----------------------------------------------------------
# Okay, here we actually start doing the installation.

# Create and set up the directory containing the "front-end" html file.

echo "Creating the "front-end" directory ($pephome)"
set targetdir = $pephome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rx $targetdir
cp -p bmrb_select.html $targetdir
chmod a+r $targetdir/bmrb_select.html


# Create and set up the dynamic sessions directory.

echo "Creating the dynamic sessions directory ($sessionhome)"
set targetdir = $sessionhome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rwx $targetdir
cp -p dynamic_sessions/*.base $targetdir
chmod a+r $targetdir/*.base

cp -p dynamic_sessions/*.str $demohome/session.js/bmrb
chmod a+r $demohome/session.js/bmrb/*.str


# Create and set up the data/html directory.

echo "Creating the data/html directory ($datahome)"
set targetdir = $datahome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rwx $targetdir
cp -p html/* $targetdir
chmod a+r $targetdir/help*
chmod a+r $targetdir/*.jpg
chmod a+r $targetdir/*.gif
chmod a+r $targetdir/*.txt
chmod a+r $targetdir/label.dat
chmod a+r $targetdir/*.base
#find $targetdir -name \* -type f -exec chmod a+r {} \;


# Create the link that we use to have the browser find the summary page.

echo "Creating html link ($pephome/html)"
set targetdir = $pephome/html
if (! -e $targetdir) then
  ln -s $datahome $targetdir
endif


# Create and set up the CGI directory.

echo "Creating CGI directory ($cgihome)"
set targetdir = $cgihome
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rx $targetdir
cp -p cgi-bin/make_view $targetdir
chmod a+rx $targetdir/make_view
cp -p cgi-bin/s2d $targetdir
chmod a+rx $targetdir/s2d
cp -p cgi-bin/set_modes $targetdir
chmod a+rx $targetdir/set_modes
cp -p cgi-bin/s2d.props $targetdir
chmod a+rx $targetdir/s2d.props
cp -p cgi-bin/connections $targetdir
chmod a+rx $targetdir/connections

cd cgi-bin
# Line below only works with GNU tar.
# tar cv bin | tar xv --directory=$targetdir

tar cvf bin.tar bin
set curdir = `pwd`
cd $targetdir
tar xvf $curdir/bin.tar
cd $curdir
rm bin.tar

cd ..
chmod a+rx $targetdir/bin
chmod a+rx $targetdir/bin/java


# Link data directory to CGI directory.

echo "Linking data directory to CGI directory"
set targetdir = $cgihome/data_dir
if (! -e $targetdir) then
  ln -s $datahome $targetdir
endif


# Link session directory to CGI directory.

echo "Linking session directory to CGI directory"
set targetdir = $cgihome/session_dir
if (! -e $targetdir) then
  ln -s $sessionhome $targetdir
endif


# Link data directory to the DEVise demo area.

echo "Linking data directory to DEVise demo area"
set targetdir = $demohome/dat/bmrb/dynamic_data
if (! -e $targetdir) then
  ln -s $datahome $targetdir
endif


# Link session directory to the DEVise demo area.

echo "Linking session directory to DEVise demo area"
set targetdir = $demohome/session.js/bmrb/dynamic_sessions
if (! -e $targetdir) then
  ln -s $sessionhome $targetdir
endif


# Create and set up the java classes directory.

echo "Creating java classes directory for CGI ($cgihome/java_classes)"
set targetdir = $cgihome/java_classes
if (! -e $targetdir) then
  mkdir $targetdir
endif
chmod a+rx $targetdir
cp java_classes/*.class $targetdir
cp java_classes/*.jar $targetdir
chmod a+r $targetdir/*.*


# Copy the schemas to the demo area.

echo "Copying schemas to the DEVise demo area"
cp schema/bmrb* $demohome/schema/schema/physical

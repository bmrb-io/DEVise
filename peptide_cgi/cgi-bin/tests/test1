#! /bin/csh -f

# Tests both BMRB and PDB processing with local files, so we can run this
# test without a web connection.

set result = 0

ln -s test_data/bmr4267.str .
ln -s test_data/1dfv.cif.gz .

rm -r -f */4267*
mkdir session_dir/4267
set coord_file = "session_dir/4267/4267ac1.ds"
touch $coord_file


mkdir html_dir/4267
set html_file = "html_dir/4267/4267ac1.html"
touch $html_file

echo -n "Processing NMR-STAR: "
./s2d -nomail -bmrbid 4267 -local -force -check_res_list \
  -lacs_file test_data/bmr4267_LACS.str -check_pct
if ($status != 0) then
  echo "First invocation of s2d failed!"
  set result = 1
endif
mv tmp/s2d.out tmp/s2d.out1

if (-f $coord_file) then
  echo "Error: $coord_file was not deleted!"
  set result = 1
endif

if (-f $html_file) then
  echo "Error: $html_file was not deleted!"
  set result = 1
endif

echo -n "Processing mmCIF: "
./s2d -nomail -pdbid 1dfv -bmrbid 4267 -do_pdb 2 -coord_index 1 -local -force \
  -check_res_list -check_pct
if ($status != 0) then
  echo "Second invocation of s2d failed!"
  set result = 1
endif

rm bmr4267.str
rm 1dfv.cif.gz


source tests/test1_check


echo -n "Test1 result: "
if ($result == 0) then
  echo "OK"
  exit 0
else
  echo "FAILED"
  exit 1
endif

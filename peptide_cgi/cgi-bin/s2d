#! /bin/csh -f

set domail = 1

if (xx$1 == "xx-nomail") then
  set domail = 0
  shift
endif


limit filesize 30000
# We need a big cpu time limit for 4096.
limit cputime 1000
limit coredumpsize 0

unsetenv LD_LIBRARY_PATH

set outbase = tmp/s2d.out
set outfile = $outbase.p$$
rm -f $outfile

set path = ($path .)
# Note: csr_timeout is in front of the args passed in so whatever calls
# this script can override the value set here.
bin/java -classpath ./s2d.jar star2devise.S2DMain \
  -csr_timeout 120 $* \
  -data_dir data_dir -session_dir session_dir -html_dir html_dir \
  -conn_file chem_info/connections >& $outfile
set failed = $status
# Exit code of 2 means error email should not be sent even though an
# error occurred (because it's a user error rather than a program error).
if ($failed > 1) then
  echo "Peptide-CGI failed, but error email disabled by exit code"
  set domail = 0
endif

chmod 644 $outfile

if ($failed && $domail) then
  set header = "Error message from s2d on `hostname`"

  set errfile = data_dir/s2d_err.p$$
  echo $header > $errfile
  echo "" >> $errfile
  echo "Arguments: $*" >> $errfile
  echo "" >> $errfile
  bin/java star2devise.S2DMain -version >> $errfile
  echo "" >> $errfile
  cat $outfile >> $errfile

  cat $errfile | mail -s "$header" `cat error_email`
  rm -f $errfile
endif

rm -f $outbase
mv $outfile $outbase

if ($failed) then
  echo FAILED
  exit 1
else
  echo OK
endif

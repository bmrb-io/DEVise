#! /bin/csh -f

# This is the highest-level script for viewing an unreleased entry.
# The user gives a BMRB accession number, and this script takes
# care of everything else.  (It calls vur to handle the actual Star
# to DEVise conversion.)

# --------------------------------------------------------

if ($#argv < 1 || $#argv > 3 || xx$1 == xx-usage) then
  echo "Usage: view_unreleased [-np] <accession number> [input file]"
  echo "File path, if specified, must be relative to the Third.Ed directory"
  exit
endif


set do_processing = 1
if ($1 == "-np") then
  set do_processing = 0
  shift
endif


# Following commands have to be run in this directory.
cd `dirname $0`


# Get the accession number.
set accnum = $1


# Get various configuration settings.
source vur_config

set sess_dir = session_dir/.unreleased/bmr$accnum


if ($do_processing) then

  # Set up input file names and scp over the input file.
  if ($#argv > 1) then
    set in_file = $2
  else
    set in_file = bmr$accnum.str
  endif
  set in_path = $input_dir/$in_file
  set tmpfile = tmp/bmr$accnum.str

  scp $in_path $tmpfile
  if ($status) then
    echo -n "Copying $in_file failed..."
    set in_file = bmr$accnum.str.nmrstr
    set in_path = $input_dir/$in_file
    set tmpfile = tmp/bmr$accnum.str.nmrstr
    echo "trying $in_file"

    scp $in_path $tmpfile
    if ($status) then
      echo "Unable to copy input file; exiting"
      exit 1
    endif
  endif


  # Create the directory for the sessions, if necessary.
  if (! -e $sess_dir) then
    mkdir $sess_dir
  else
    rm $sess_dir/*
  endif


  # Now actually do the processing.
  set name = $accnum"ur_"

  # TEMPTEMP -- check whether it worked
  ./vur -file $tmpfile -name $name -session_dir $sess_dir -do_csr $csr_lev


  # Remove the input file.
  rm $tmpfile

else

  echo "No processing because of -np flag -- using existing visualizations"

endif


# Now give the user a choice of viewing all of the available sessions.
# Note:  we should have a better user interface for this!
set sessions = `ls $sess_dir/*.ds | cut -d / -f 4`

echo ""
echo -n "Available sessions: "
foreach session ($sessions)
  echo -n "$session "
end
echo ""
echo ""

cd $js_dir
foreach session ($sessions)
  echo -n "View session $session (y/n/q)? "
  set answer = $<

  if ($answer == q || $answer == Q) then
    exit
  endif

  while ($answer != y && $answer != Y && $answer != n && $answer != N)
    echo -n "I didn't understand your answer -- please answer y or n: "
    set answer = $<
  end
  if ($answer == y || $answer == Y) then
    ./js -sessionbmrb/dynamic_sessions/.unreleased/bmr$accnum/$session
  endif
end
